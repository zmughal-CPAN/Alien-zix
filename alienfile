use alienfile;

plugin PkgConfig => 'zix-0';

share {
  requires 'Alien::Build::Plugin::Download::GitLab';
  requires 'Alien::Meson';
  requires 'Alien::Ninja';

  # https://gitlab.com/drobilla/zix
  plugin 'Download::GitLab' => (
    gitlab_user    => 'drobilla',
    gitlab_project => 'zix',
  );

  plugin Extract => 'tar.gz';

  my $build_dir = '_build';
  build [
    sub {
      my $build = shift;
      Alien::Build::CommandSequence->new([
        Alien::Meson->exe, 'setup',
          (
            $^O ne 'MSWin32'
            ? ( '-Ddefault_library=both', ) # both static and shared
            : ()
          ),
          '--prefix=%{.install.prefix}',
          '--libdir=lib',
          '--buildtype=release',
          '-Ddocs=disabled', # no docs
          $build_dir,
      ])->execute($build);
    },
    [ '%{ninja}', qw(-C), $build_dir, "test" ],
    [ '%{ninja}', qw(-C), $build_dir, 'install' ],
  ];

  if( $^O ne 'MSWin32' ) {
    plugin 'Gather::IsolateDynamic';
  }
}
